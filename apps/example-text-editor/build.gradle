plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

apply from: '../apps.gradle'

ext.depsRoot = 'XbupToolsJava'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.xbup.tool.example.text.editor.TextEditorApp'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.mainClass = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'core', 'exbin-framework')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'core', 'exbin-framework-basic')
    implementation moduleDep('XbupToolsJava', 'org.exbin.framework', 'modules', 'exbin-tool-example-text-editor-launcher')
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { '../lib/' + it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

