// Shared apps build

version = '0.3.0-SNAPSHOT'
def repositoryName = "xbup-tools-java"
def libraryUrl = 'https://xbup.exbin.org'

ext.depsThis = 'XbupToolsJava'

ext.depsBined = [
    githubProfile: 'exbin',
    githubRepo: 'bined',
    version: '0.3.0-SNAPSHOT'
]

ext.depsBinedLibJava = [
    githubProfile: 'exbin',
    githubRepo: 'bined-lib-java',
    version: '0.3.0-SNAPSHOT'
]

ext.depsExbinFrameworkJava = [
    githubProfile: 'exbin',
    githubRepo: 'exbin-framework-java',
    version: '0.3.0-SNAPSHOT'
]

ext.depsXbupJava = [
    githubProfile: 'exbin',
    githubRepo: 'xbup-java',
    version: '0.3.0-SNAPSHOT'
]

ext.depsExbinAuxiliaryJava = [
    githubProfile: 'exbin',
    githubRepo: 'exbin-auxiliary-java',
    version: '0.3.0-SNAPSHOT'
]

apply from: '../deps_processing.gradle'

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
    }
}

// Disable tests on build
if (!gradle.startParameter.taskNames.any {it.endsWith("test")}) {
    tasks.withType(Test) {enabled = false}
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier = 'tests'
    archiveBaseName = "test-${project.name}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.java
}

javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
