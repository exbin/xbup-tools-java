plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

apply from: '../apps.gradle'

ext {
    depsRoot = 'XbupToolsJava'
    appName = 'XBUP Manager'
    vendor = 'ExBin Project'
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.xbup.tool.manager.ManagerApp'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.mainClass = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    implementation moduleDep('XbupJava', 'org.exbin.xbup', 'modules', 'xbup-core')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'core', 'exbin-framework')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'core', 'exbin-framework-basic')
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'core', 'exbin-framework-addon')
    implementation moduleDep('Bined', 'org.exbin.framework', 'modules', 'exbin-framework-bined')
    implementation moduleDep('Bined', 'org.exbin.framework', 'modules', 'exbin-framework-bined-inspector')
    implementation moduleDep('Bined', 'org.exbin.framework', 'modules', 'exbin-framework-bined-search')
    implementation moduleDep('XbupToolsJava', 'org.exbin.framework', 'modules', 'exbin-framework-xbup-catalog')
    implementation moduleDep('XbupToolsJava', 'org.exbin.framework', 'modules', 'exbin-framework-xbup-service')
    implementation moduleDep('XbupToolsJava', 'org.exbin.framework', 'modules', 'exbin-framework-editor-xbup')
    implementation moduleDep('XbupToolsJava', 'org.exbin.framework', 'modules', 'exbin-tool-manager-launcher')
    implementation 'commons-cli:commons-cli:1.2'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.6.2'
    compileOnly 'org.eclipse.persistence:javax.persistence:2.2.1'
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    runtimeOnly 'org.eclipse.persistence:eclipselink:2.7.7'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { '../lib/' + it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath(),
            'Build-JDK': System.getProperty('java.version'),
            'Specification-Title': project.appName,
            'Specification-Version': project.version,
            'Specification-Vendor': project.vendor
        )
    }
}

