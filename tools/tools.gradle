// Shared tools build

apply plugin: 'java'

sourceCompatibility = '1.7'
version = '0.2.0-SNAPSHOT'
def repositoryName = "xbup-tools-java"
def libraryUrl = 'http://xbup.exbin.org'
ext.xbupVersion = '0.2.0-SNAPSHOT'
ext.frameworkVersion = '0.2.0-SNAPSHOT'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
        options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
        options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
    }
}

// Disable tests on build
check.dependsOn.remove(test)

task testJar(type: Jar, dependsOn: testClasses) {
    classifier "tests"
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


