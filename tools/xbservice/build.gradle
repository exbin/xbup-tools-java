apply from: '../tools.gradle'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("at.bxm.gradleplugins:gradle-svntools-plugin:latest.integration")
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.xbup.service.XBService'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    compile moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework')
    compile moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-core')
    compile moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-client')
    compile moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-service')
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.eclipse.persistence:eclipselink:2.5.1'
    runtime 'mysql:mysql-connector-java:5.1.30'
    runtime 'org.eclipse.persistence:javax.persistence:2.1.0'
    runtime 'org.apache.derby:derbyclient:10.12.1.1'
    runtime 'org.apache.derby:derby:10.12.1.1'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

def manifestClassPath() {
    try {
        return (configurations.runtime.collect { '../lib/' + it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

