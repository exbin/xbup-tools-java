apply from: '../tools.gradle'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.xbup.service.XBService'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    compile "org.exbin.xbup:xbup-core:${xbupVersion}"
    compile "org.exbin.xbup:xbup-client:${xbupVersion}"
    compile "org.exbin.xbup:xbup-service:${xbupVersion}"
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.eclipse.persistence:eclipselink:2.5.1'
    runtime 'mysql:mysql-connector-java:5.1.30'
    runtime 'org.eclipse.persistence:javax.persistence:2.1.0'
    runtime 'org.apache.derby:derbyclient:10.12.1.1'
    runtime 'org.apache.derby:derby:10.12.1.1'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': configurations.runtime.collect {'../lib/' + it.getName() }.join(' ')
        )
    }
}
