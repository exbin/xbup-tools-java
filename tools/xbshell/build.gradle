apply from: '../tools.gradle'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("at.bxm.gradleplugins:gradle-svntools-plugin:latest.integration")
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.xbup.tool.xbshell.XBShell'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    mavenCentral()
    mavenLocal()
    flatDir {
        dirs "../../lib"
    }
}

dependencies {
    implementation moduleDep('XbupJava', 'org.exbin.xbup', 'xbup-core')
    implementation 'commons-cli:commons-cli:1.2'
    runtimeOnly 'org.apache.derby:derbyclient:10.12.1.1'
    testImplementation group: 'junit', name: 'junit', version: '4.10'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { '../lib/' + it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

